import java.text.SimpleDateFormat

//注意gradle.properties中的数据类型都是String类型，使用其他数据类型需要自行转换
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion build_versions.target_sdk
    defaultConfig {
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk

        applicationId "com.sunny.module.home"

        versionCode 100
        versionName "1.0"

        kapt {
            arguments {
                arg("AROUTER_MODULE_NAME", project.getName())
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions('type')
    productFlavors {
        hpc {
            dimension 'type'
        }
        mpc {
            dimension 'type'
        }
    }
    sourceSets {
        // 对应productFlavors的名称
        hpc {
            manifest.srcFile 'src/main/hpc/AndroidManifest.xml'
        }
        mpc {
            manifest.srcFile 'src/main/mpc/AndroidManifest.xml'
        }
    }
    // 给生成的apk重命名
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def formattedDate = new SimpleDateFormat("yyyy-MM-dd").format(new Date())
            def fileName = "${variant.name}-${variant.versionName}-${formattedDate}.apk"
            output.outputFileName = fileName
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        dataBinding true
    }
}

dependencies {
    implementation project(path: ':libCommon')
//    implementation project(path: ':module_stu')

    // 路由
    kapt deps.arouter.compiler
}