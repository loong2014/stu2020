//注意gradle.properties中的数据类型都是String类型，使用其他数据类型需要自行转换
if (isModuleViewAlone.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {

    compileSdkVersion build_versions.target_sdk
    defaultConfig {
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk

        if (isModuleViewAlone.toBoolean()) {
            applicationId "com.sunny.module.view"
        }

        versionCode 100
        versionName "1.0"

        // router
        kapt {
            arguments {
                arg("AROUTER_MODULE_NAME", project.getName())
            }
        }
    }

    sourceSets {
        main {
            if (isModuleViewAlone.toBoolean()) {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/moduleManifest/AndroidManifest.xml'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    // 添加Databinding
    buildFeatures {
        dataBinding true
    }

    /**
     * 通过resourcePrefix限定当前module可访问资源
     */
    resourcePrefix "view_"
}

dependencies {
    implementation project(path: ':libCommon')

    // 路由
    kapt deps.arouter.compiler

    implementation deps.glide.runtime
    kapt deps.glide.compiler

    // 圆形图片 https://github.com/hdodenhof/CircleImageView
    implementation 'de.hdodenhof:circleimageview:3.1.0'

    // https://developer.android.google.cn/jetpack/androidx/releases/cardview
    implementation 'androidx.cardview:cardview:1.0.0'

}